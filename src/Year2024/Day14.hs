module Year2024.Day14 (solve) where

import Data.Foldable (foldl')
import Data.List.Split (splitOn)
import Data.Maybe (mapMaybe)
import Grid (Grid2D (Grid2D), Position (Position), setAllGridAt)

{- | Part 1

One of The Historians needs to use the bathroom; fortunately, you know there's
a bathroom near an unvisited location on their list, and so you're all quickly
teleported directly to the lobby of Easter Bunny Headquarters.

Unfortunately, EBHQ seems to have "improved" bathroom security again after your
last visit. The area outside the bathroom is swarming with robots!

To get The Historian safely to the bathroom, you'll need a way to predict where
the robots will be in the future. Fortunately, they all seem to be moving on the
tile floor in predictable straight lines.

You make a list (your puzzle input) of all of the robots' current positions (p)
and velocities (v), one robot per line. For example:

p=0,4 v=3,-3
p=6,3 v=-1,-3
p=10,3 v=-1,2
p=2,0 v=2,-1
p=0,0 v=1,3
p=3,0 v=-2,-2
p=7,6 v=-1,-3
p=3,0 v=-1,-2
p=9,3 v=2,3
p=7,3 v=-1,2
p=2,4 v=2,-3
p=9,5 v=-3,-3

Each robot's position is given as p=x,y where x represents the number of tiles
the robot is from the left wall and y represents the number of tiles from the
top wall (when viewed from above). So, a position of p=0,0 means the robot is
all the way in the top-left corner.

Each robot's velocity is given as v=x,y where x and y are given in tiles per
second. Positive x means the robot is moving to the right, and positive y means
the robot is moving down. So, a velocity of v=1,-2 means that each second, the
robot moves 1 tile to the right and 2 tiles up.

The robots outside the actual bathroom are in a space which is 101 tiles wide
and 103 tiles tall (when viewed from above). However, in this example, the
robots are in a space which is only 11 tiles wide and 7 tiles tall.

The robots are good at navigating over/under each other (due to a combination of
springs, extendable legs, and quadcopters), so they can share the same tile and
don't interact with each other. Visually, the number of robots on each tile in
this example looks like this:

1.12.......
...........
...........
......11.11
1.1........
.........1.
.......1...

These robots have a unique feature for maximum bathroom security: they can
teleport. When a robot would run into an edge of the space they're in, they
instead teleport to the other side, effectively wrapping around the edges. Here
is what robot p=2,4 v=2,-3 does for the first few seconds:

Initial state:
...........
...........
...........
...........
..1........
...........
...........

After 1 second:
...........
....1......
...........
...........
...........
...........
...........

After 2 seconds:
...........
...........
...........
...........
...........
......1....
...........

After 3 seconds:
...........
...........
........1..
...........
...........
...........
...........

After 4 seconds:
...........
...........
...........
...........
...........
...........
..........1

After 5 seconds:
...........
...........
...........
.1.........
...........
...........
...........

The Historian can't wait much longer, so you don't have to simulate the robots
for very long. Where will the robots be after 100 seconds?

In the above example, the number of robots on each tile after 100 seconds has
elapsed looks like this:

......2..1.
...........
1..........
.11........
.....1.....
...12......
.1....1....

To determine the safest area, count the number of robots in each quadrant after
100 seconds. Robots that are exactly in the middle (horizontally or vertically)
don't count as being in any quadrant, so the only relevant robots are:

..... 2..1.
..... .....
1.... .....

..... .....
...12 .....
.1... 1....

In this example, the quadrants contain 1, 3, 4, and 1 robot. Multiplying these
together gives a total safety factor of 12.

Predict the motion of the robots in your list within a space which is 101 tiles
wide and 103 tiles tall. What will the safety factor be after exactly 100
seconds have elapsed?

Example should be 12:

>>> let example = "p=0,4 v=3,-3\np=6,3 v=-1,-3\np=10,3 v=-1,2\np=2,0 v=2,-1\np=0,0 v=1,3\np=3,0 v=-2,-2\np=7,6 v=-1,-3\np=3,0 v=-1,-2\np=9,3 v=2,3\np=7,3 v=-1,2\np=2,4 v=2,-3\np=9,5 v=-3,-3"
>>> part1 example (Bounds 11 7)
12
-}
part1 :: String -> Bounds -> Int
part1 s b =
    safetyFactor
        . map (advanceRobot 100)
        . mapMaybe (`parseRobot` b)
        . lines
        $ s

safetyFactor :: [Robot] -> Int
safetyFactor rs = rs1 * rs2 * rs3 * rs4
  where
    (q1, q2, q3, q4) = toQuadrants . getBounds . head $ rs
    rs1 = length $ filter (`isIn` q1) rs
    rs2 = length $ filter (`isIn` q2) rs
    rs3 = length $ filter (`isIn` q3) rs
    rs4 = length $ filter (`isIn` q4) rs

data Robot = Robot Position Velocity Bounds deriving (Show, Eq)

getBounds :: Robot -> Bounds
getBounds (Robot _ _ b) = b

data Bounds = Bounds Int Int deriving (Show, Eq)

data Quadrant = Quadrant (Int, Int) (Int, Int) deriving (Show, Eq)

data Velocity = Velocity Int Int deriving (Show, Eq)

toQuadrants :: Bounds -> (Quadrant, Quadrant, Quadrant, Quadrant)
toQuadrants (Bounds x y) =
    ( Quadrant (0, hx) (0, hy)
    , Quadrant (x - hx, x) (0, hy)
    , Quadrant (0, hx) (y - hy, y)
    , Quadrant (x - hx, x) (y - hy, y)
    )
  where
    hx = x `div` 2
    hy = y `div` 2

isIn :: Robot -> Quadrant -> Bool
isIn (Robot (Position x y) _ _) (Quadrant (x1, x2) (y1, y2)) =
    (x1 <= x && x < x2) && (y1 <= y && y < y2)

-- >>> advanceRobot 5 (Robot (Position 2 4) (Velocity 2 (-3)) (Bounds 11 7))
-- Robot (Position 1 3) (Velocity 2 (-3)) (Bounds 11 7)
advanceRobot :: Int -> Robot -> Robot
advanceRobot n r = foldl' (\r' _ -> robotStep r') r [0 .. (n - 1)]

-- >>> robotStep (Robot (Position 2 4) (Velocity 2 (-3)) (Bounds 11 7))
-- Robot (Position 4 1) (Velocity 2 (-3)) (Bounds 11 7)
robotStep :: Robot -> Robot
robotStep (Robot (Position x y) v@(Velocity vx vy) b@(Bounds bx by)) =
    Robot (Position ((x + vx) `mod` bx) ((y + vy) `mod` by)) v b

-- >>> parseRobot "p=0,4 v=3,-3" (Bounds 11 7)
-- Just (Robot (Position 0 4) (Velocity 3 (-3)))
parseRobot :: String -> Bounds -> Maybe Robot
parseRobot s b = do
    [pos, vel] <- Just $ words s
    [px, py] <- Just . map read . splitOn "," . drop 2 $ pos
    [vx, vy] <- Just . map read . splitOn "," . drop 2 $ vel
    return $ Robot (Position px py) (Velocity vx vy) b

{- | Part 2

During the bathroom break, someone notices that these robots seem awfully
similar to ones built and used at the North Pole. If they're the same type of
robots, they should have a hard-coded Easter egg: very rarely, most of the
robots should arrange themselves into a picture of a Christmas tree.

What is the fewest number of seconds that must elapse for the robots to display
the Easter egg?

Solution:

Second: 6870
..............................X......................................................................
.........................................................................X...........................
.....................................................................................................
.......................................................................X..........................X..
................................................X...........X............X........X..................
..................................X................................X..X..............................
.......................................................................X.............................
X....................................................................................................
..X....................................................X.............................................
................X....X...................X.....X....................................X................
.....................................................................................................
.....................................................................................................
..........................X...................X......................................................
.....................................................................................X..X............
.......................X............................X................................................
......................................X.........X............................X.......X...............
...........................X.........................................................................
..........................................................................X..........................
.......................................X.................................X....................X....X.
.....................................................................................................
............................................................................X........................
.....................................................................................................
...................................................................X..............................X..
..............X.........................................X........XX..................................
.............................X..............X........................................................
.....................................................................................................
.....................................................................................................
..................X...................XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX...X...........................X
......................................X.............................X.X..............................
......................................X.............................X................................
...X..................................X.............................X.............................X..
...............X......................X.............................X................................
......................................X..............X..............X......................X.........
......................................X.............XXX.............X................................
....X.................................X............XXXXX............X................................
......X.......................X.......X...........XXXXXXX...........X................................
......................................X..........XXXXXXXXX..........X................................
..................X...................X............XXXXX............X................................
..........X...........................X...........XXXXXXX...........X............................X...
......................................X..........XXXXXXXXX..........X................................
...........................X..........X.........XXXXXXXXXXX.........X................................
.....................X................X........XXXXXXXXXXXXX........X.....X..........................
....................................X.X..........XXXXXXXXX..........X................................
......................................X.........XXXXXXXXXXX.........X................................
......................................X........XXXXXXXXXXXXX........X.......X........................
........X.............................X.......XXXXXXXXXXXXXXX.......X................................
......................................X......XXXXXXXXXXXXXXXXX......X................................
...............................X......X........XXXXXXXXXXXXX........X................................
......................................X.......XXXXXXXXXXXXXXX.......X...........X....................
.......X..............................X......XXXXXXXXXXXXXXXXX......X...............X.....X..........
......................................X.....XXXXXXXXXXXXXXXXXXX.....X................................
......................................X....XXXXXXXXXXXXXXXXXXXXX....X.X............X.............X...
......................................X.............XXX.............X................................
....X.................................X.............XXX.............X.X..............................
...............X......................X.............XXX.............X................................
.................X....................X.............................X..................X.............
......................................X.............................X................................
.................X...........X........X.............................X................................
..............X.......................X.............................X................................
........X.............................XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX................................
.....................................................................................................
.....................................................................................................
........X............................................................................................
...X...........X...................................................................................X.
.....................................................................................................
.....................................................................................................
.............................................................................X.......................
...................X......X...............................................X..........................
..........................................................X..........................................
.................................................................................X...................
...............................X.......................X.............................................
.....................................................................................................
.....................................................................................................
.....................................................................................................
....................................................................X................................
....X.....................X................................X.........................................
................................................X.................................................X..
.................................................X........................................X..........
............X........................................................................................
.......................................................................X..........................X..
...X...............................................................X.................................
.....................................................................................................
.X...........................X..................................................X....................
..................X....................X....X..............................X..................X......
............................................X.............................................X..........
.............X.......X.......................................................X.......................
..................X..................X..........X.....................X..............................
...X.....X....................X............................................X.........................
............XX.......................................................................................
........................X.....................................X......................................
.........................X...........................................................................
............................................................X.........X..............................
.....................................................................................................
......................................................X.......X..........X...........................
................X....................................................................................
.....................................................................................................
.....................................................................................................
.........................................................X...........................................
X....................................................................................................
......................X..............................................................................
........X...............................................X....X.......................................
.X..............X....................................................................................
.....................................................................................................
-}
part2 :: String -> Bounds -> IO ()
part2 s b =
    mapM_
        (\i -> printMap (getR i) . map (advanceRobot (getR i)) $ robots)
        [0 .. 100]
  where
    robots = mapMaybe (`parseRobot` b) . lines $ s
    getR i = i * 100 + i + 2

printMap :: Int -> [Robot] -> IO ()
printMap i rs = case pGrid of
    Just (Grid2D ls) -> putStrLn $ unlines (heading : ls)
    Nothing -> putStrLn "Nothing"
  where
    heading = "Second: " ++ show i
    grid = Grid2D $ replicate 103 (replicate 101 '.')
    pGrid = setAllGridAt 'X' (map getPosition rs) grid

getPosition :: Robot -> Position
getPosition (Robot p _ _) = p

solve :: IO ()
solve = do
    content <- readFile "./src/Year2024/data/day14.txt"
    print $ "Part1 solution: " ++ show (part1 content (Bounds 101 103))
    print "Part2 solution:"
    part2 content (Bounds 101 103)
