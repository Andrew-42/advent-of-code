module Year2025.Day07 (solve) where

import Data.Maybe (fromJust)
import qualified Data.Set as S
import qualified Data.Text as T
import qualified Data.Text.IO as TIO
import Grid (
    Direction (East, North, West),
    Grid2D (Grid2D),
    Position,
    Shape (nRows),
    gridAt,
    next,
    positionOf,
    row,
    shape,
 )
import Utils (groupBy)

{- | Part 1

--- Day 7: Laboratories ---

You thank the cephalopods for the help and exit the trash compactor, finding yourself in the
familiar halls of a North Pole research wing.

Based on the large sign that says "teleporter hub", they seem to be researching teleportation; you
can't help but try it for yourself and step onto the large yellow teleporter pad.

Suddenly, you find yourself in an unfamiliar room! The room has no doors; the only way out is the
teleporter. Unfortunately, the teleporter seems to be leaking magic smoke.

Since this is a teleporter lab, there are lots of spare parts, manuals, and diagnostic equipment
lying around. After connecting one of the diagnostic tools, it helpfully displays error code 0H-N0,
which apparently means that there's an issue with one of the tachyon manifolds.

You quickly locate a diagram of the tachyon manifold (your puzzle input). A tachyon beam enters the
manifold at the location marked S; tachyon beams always move downward. Tachyon beams pass freely
through empty space (.). However, if a tachyon beam encounters a splitter (^), the beam is stopped;
instead, a new tachyon beam continues from the immediate left and from the immediate right of the
splitter.

For example:

.......S.......
...............
.......^.......
...............
......^.^......
...............
.....^.^.^.....
...............
....^.^...^....
...............
...^.^...^.^...
...............
..^...^.....^..
...............
.^.^.^.^.^...^.
...............

In this example, the incoming tachyon beam (|) extends downward from S until it reaches the first
splitter:

.......S.......
.......|.......
.......^.......
...............
......^.^......
...............
.....^.^.^.....
...............
....^.^...^....
...............
...^.^...^.^...
...............
..^...^.....^..
...............
.^.^.^.^.^...^.
...............

At that point, the original beam stops, and two new beams are emitted from the splitter:

.......S.......
.......|.......
......|^|......
...............
......^.^......
...............
.....^.^.^.....
...............
....^.^...^....
...............
...^.^...^.^...
...............
..^...^.....^..
...............
.^.^.^.^.^...^.
...............

Those beams continue downward until they reach more splitters:

.......S.......
.......|.......
......|^|......
......|.|......
......^.^......
...............
.....^.^.^.....
...............
....^.^...^....
...............
...^.^...^.^...
...............
..^...^.....^..
...............
.^.^.^.^.^...^.
...............

At this point, the two splitters create a total of only three tachyon beams, since they are both
dumping tachyons into the same place between them:

.......S.......
.......|.......
......|^|......
......|.|......
.....|^|^|.....
...............
.....^.^.^.....
...............
....^.^...^....
...............
...^.^...^.^...
...............
..^...^.....^..
...............
.^.^.^.^.^...^.
...............

This process continues until all of the tachyon beams reach a splitter or exit the manifold:

.......S.......
.......|.......
......|^|......
......|.|......
.....|^|^|.....
.....|.|.|.....
....|^|^|^|....
....|.|.|.|....
...|^|^|||^|...
...|.|.|||.|...
..|^|^|||^|^|..
..|.|.|||.|.|..
.|^|||^||.||^|.
.|.|||.||.||.|.
|^|^|^|^|^|||^|
|.|.|.|.|.|||.|

To repair the teleporter, you first need to understand the beam-splitting properties of the tachyon
manifold. In this example, a tachyon beam is split a total of 21 times.

Analyze your manifold diagram. How many times will the beam be split?

Example:

>>> let example = ".......S.......\n...............\n.......^.......\n...............\n......^.^......\n...............\n.....^.^.^.....\n...............\n....^.^...^....\n...............\n...^.^...^.^...\n...............\n..^...^.....^..\n...............\n.^.^.^.^.^...^.\n..............."
>>> part1 example
21
-}
part1 :: T.Text -> Int
part1 text = go (S.singleton $ start grid) 0
  where
    go :: S.Set Position -> Int -> Int
    go ps collisions
        | row (S.elemAt 0 ps) == maxRow = collisions
        | otherwise = go passedPs (collisions + nSplits)
      where
        passedPs = S.fromList . concatMap (pass grid) . down . S.toList $ ps
        nSplits = length . filter (isSplitter grid) . down . S.toList $ ps

    maxRow = nRows (shape grid)
    grid = pInput text

start :: Grid2D Char -> Position
start g = fromJust $ positionOf 'S' g

down :: [Position] -> [Position]
down = map (`next` North)

pass :: Grid2D Char -> Position -> [Position]
pass g p = if isSplitter g p then split g p else [p]

isSplitter :: Grid2D Char -> Position -> Bool
isSplitter g p = case gridAt p g of
    Just '^' -> True
    _ -> False

split :: Grid2D Char -> Position -> [Position]
split g p = filter (isEmpty g) [next p West, next p East]

isEmpty :: Grid2D Char -> Position -> Bool
isEmpty g p = case gridAt p g of
    Just '.' -> True
    _ -> False

{- | Part 2

With your analysis of the manifold complete, you begin fixing the teleporter. However, as you open
the side of the teleporter to replace the broken manifold, you are surprised to discover that it
isn't a classical tachyon manifold - it's a quantum tachyon manifold.

With a quantum tachyon manifold, only a single tachyon particle is sent through the manifold.
A tachyon particle takes both the left and right path of each splitter encountered.

Since this is impossible, the manual recommends the many-worlds interpretation of quantum tachyon
splitting: each time a particle reaches a splitter, it's actually time itself which splits. In one
timeline, the particle went left, and in the other timeline, the particle went right.

To fix the manifold, what you really need to know is the number of timelines active after a single
particle completes all of its possible journeys through the manifold.

In the above example, there are many timelines. For instance, there's the timeline where the
particle always went left:

.......S.......
.......|.......
......|^.......
......|........
.....|^.^......
.....|.........
....|^.^.^.....
....|..........
...|^.^...^....
...|...........
..|^.^...^.^...
..|............
.|^...^.....^..
.|.............
|^.^.^.^.^...^.
|..............

Or, there's the timeline where the particle alternated going left and right at each splitter:

.......S.......
.......|.......
......|^.......
......|........
......^|^......
.......|.......
.....^|^.^.....
......|........
....^.^|..^....
.......|.......
...^.^.|.^.^...
.......|.......
..^...^|....^..
.......|.......
.^.^.^|^.^...^.
......|........

Or, there's the timeline where the particle ends up at the same point as the alternating timeline,
but takes a totally different path to get there:

.......S.......
.......|.......
......|^.......
......|........
.....|^.^......
.....|.........
....|^.^.^.....
....|..........
....^|^...^....
.....|.........
...^.^|..^.^...
......|........
..^..|^.....^..
.....|.........
.^.^.^|^.^...^.
......|........

In this example, in total, the particle ends up on 40 different timelines.

Apply the many-worlds interpretation of quantum tachyon splitting to your manifold diagram.
In total, how many different timelines would a single tachyon particle end up on?

Example:

>>> let example = ".......S.......\n...............\n.......^.......\n...............\n......^.^......\n...............\n.....^.^.^.....\n...............\n....^.^...^....\n...............\n...^.^...^.^...\n...............\n..^...^.....^..\n...............\n.^.^.^.^.^...^.\n..............."
>>> part2 example
-}
part2 :: T.Text -> Int
part2 text = go [(start grid, 1)]
  where
    go :: [(Position, Int)] -> Int
    go ps
        | row (fst $ head ps) == maxRow = sum . map snd $ ps
        | otherwise = go passedPs
      where
        passedPs =
            map (\ps' -> (fst . head $ ps', sum . map snd $ ps'))
                . groupBy id
                . concatMap (pass' grid)
                . down'
                $ ps

    maxRow = nRows (shape grid)
    grid = pInput text

down' :: [(Position, Int)] -> [(Position, Int)]
down' = map (\(p, n) -> (p `next` North, n))

pass' :: Grid2D Char -> (Position, Int) -> [(Position, Int)]
pass' g (p, n) =
    if isSplitter g p
        then map (\p' -> (p', n)) $ split g p
        else [(p, n)]

pInput :: T.Text -> Grid2D Char
pInput = Grid2D . lines . T.unpack

solve :: IO ()
solve = do
    content <- TIO.readFile "./src/Year2025/data/day07.txt"
    -- Part1 solution: 1605
    print $ "Part1 solution: " ++ show (part1 content)
    -- Part2 solution: 29893386035180
    print $ "Part2 solution: " ++ show (part2 content)
